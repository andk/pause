#!/opt/perl/current/bin/perl

# INIT Info Generated at Sat Apr 23 13:17:44 2016 with Daemon::Control DEV

### BEGIN INIT INFO
# Provides:          PAUSE-plack
# Required-Start:    $syslog $remote_fs
# Required-Stop:     $syslog
# Default-Start:     2 3 4 5
# Default-Stop:      0 1 6
# Short-Description: PAUSE-Plack
# Description:       PAUSE-Plack web program
### END INIT INFO`

use strict;
# use FindBin;
use Daemon::Control;

# for the record, on 20160421 we started plackperl/charsbar for live from /home/puppet/pause manually with
# 2016-04-22 16:31  /opt/perl/v5.16.2/165a/bin/plackup -I ../pause-private/lib -E production -s Starman -r -p 81 &

my $name = 'PAUSE-plack';
my $psgi = "/home/puppet/pause/app.psgi";
(my $starman = $^X) =~ s{/perl[^/]*$}{/starman};

exit My::Daemon::Control->new({
    name        => $name,
    lsb_start   => '$syslog $remote_fs',
    lsb_stop    => '$syslog',
    lsb_sdesc   => 'PAUSE-Plack',
    lsb_desc    => 'PAUSE-Plack web program',

    program     => $starman,
    program_args => [
        $psgi,
        '--workers',    15,
        '--listen',     ":81",
        '--user',       'apache',
        '--group',      'apache',
        '--preload-app',
        '--env',        'production',
        '--access-log', "/var/log/$name/access_log",
    ],

    pid_file    => "/var/run/$name.pid",
    stderr_file => "/var/log/$name/error_log",
    stdout_file => "/var/log/$name/error_log",

    fork        => 2,

})->run;

{
    package My::Daemon::Control;
    use base 'Daemon::Control';
    # See https://github.com/miyagawa/Starman/issues/94
    # and https://github.com/miyagawa/Starman/issues/106
    # why checking for a working psgi is a good idea.
    sub do_reload {
        my $self = shift;
        my $psgi_file = $self->program_args->[0];
        my $child_pid = fork;
        die if !defined $child_pid;
        if ($child_pid == 0) {
            require Plack::Util;
            Plack::Util::load_psgi($psgi_file);
            exit 0;
        }
        waitpid $child_pid, 0;
        if ($? != 0) {
            die "load_psgi of $psgi_file failed";
        }
        $self->SUPER::do_reload();
    }
}

__END__

=head1 INSTALLATION

As root:

    ./PAUSE-plack.psgi-init get_init_file > /etc/init.d/PAUSE-plack   # optional
    chmod 755 /etc/init.d/PAUSE-plack
    update-rc.d PAUSE-plack defaults

=cut
