#!/usr/bin/perl
use v5.36.0;

use Carp qw(croak);
use Getopt::Long::Descriptive;
use Path::Tiny;

if (-e '/tmp/plenv-tarball.tar.bz2') {
  chdir("/home/pause") or die "can't chdir to ~pause: $!";

  # Somebody helpfully gave us a plenv directory to start from!  Let's use it.
  run_cmd(qw(tar jxvf /tmp/plenv-tarball.tar.bz2));

  path("/home/pause/.bash_profile")->append(<<~'END');
    export PATH="$HOME/.plenv/bin:$PATH"
    eval "$(plenv init -)"
    END
} else {
  chdir("/home/pause/pause") or die "can't chdir to ~pause/pause: $!";

  # install plenv so we can manage a local perl version
  run_cmd(qw(
    git clone https://github.com/tokuhirom/plenv.git /home/pause/.plenv
  ));

  path("/home/pause/.bash_profile")->append(<<~'END');
    export PATH="$HOME/.plenv/bin:$PATH"
    eval "$(plenv init -)"
    END

  # install perl-build so we can build a new perl
  run_cmd(qw(
    git clone https://github.com/tokuhirom/Perl-Build.git
      /home/pause/.plenv/plugins/perl-build/
  ));

  run_cmd(qw( /home/pause/.plenv/bin/plenv install 5.36.0 -j 16 --noman ));
  run_cmd(qw( /home/pause/.plenv/bin/plenv global  5.36.0 ));

  # install cpanm for perl dep management
  run_cmd(qw( /home/pause/.plenv/bin/plenv install-cpanm ));

  # We need to pin these for now
  run_cmd(qw( /home/pause/.plenv/shims/cpanm -n Mojolicious@8.72 ));
  run_cmd(qw( /home/pause/.plenv/shims/cpanm -n DBD::mysql@4.052 ));

  run_cmd(qw( /home/pause/.plenv/shims/cpanm -n --installdeps . ));
}

chdir("/home/pause/pause") or die "can't chdir to ~pause/pause: $!";

# Set up pause config
for my $path (qw(
  /home/pause/incoming
  /home/pause/log
  /home/pause/pause-private/lib
  /home/pause/pid
  /home/pause/pub/PAUSE/authors/id
  /home/pause/pub/PAUSE/modules
  /home/pause/pub/PAUSE/PAUSE-git
  /home/pause/run
  /home/pause/tmp
  /tmp/pause_1999
)) {
  path($path)->mkdir;
}

chdir("/home/pause/pub/PAUSE/PAUSE-git")
  || die "couldn't chdir to PAUSE-git: $!";

run_cmd(qw(git config --global user.email pause@pause.perl.org));
run_cmd(qw(git config --global user.name), 'PAUSE Daemon');
run_cmd(qw(git init --initial-branch master ));

# This imports a test key, which nobody should trust, and which has key id
# 6BA1716EFB099DB2. -- rjbs, 2024-04-25
run_cmd(qw( gpg --import --armor /home/pause/pause/bootstrap/test-key.txt ));

path("/home/pause/pause-private/lib/PrivatePAUSE.pm")->spew(<<~'END');
  use strict;
  package PAUSE;

  $ENV{EMAIL_SENDER_TRANSPORT} = 'DevNull';

  our $Config;
  $Config->{AUTHEN_BACKUP_DIR}        = "/home/pause/db-backup";
  $Config->{AUTHEN_DATA_SOURCE_USER}  = "pause";
  $Config->{AUTHEN_DATA_SOURCE_PW}    = "pausepassword";
  $Config->{MOD_DATA_SOURCE_USER}     = "pause";
  $Config->{MOD_DATA_SOURCE_PW}       = "pausepassword";
  $Config->{MAIL_MAILER}              = ["testfile"];
  $Config->{RUNDATA}                  = "/tmp/pause_1999";

  $Config->{CHECKSUMS_SIGNING_PROGRAM} = "gpg";
  $Config->{CHECKSUMS_SIGNING_ARGS}    = '--homedir /home/pause/pause-private/gnupg-pause-batch-signing-home --clearsign --default-key ';
  $Config->{CHECKSUMS_SIGNING_KEY}     = '6BA1716EFB099DB2';

  $Config->{FTPPUB} = '/home/pause/pub/PAUSE/';
  $Config->{GITROOT} = '/home/pause/pub/PAUSE/PAUSE-git';
  $Config->{INCOMING_LOC} = '/home/pause/incoming';
  $Config->{MLROOT} = '/home/pause/pub/PAUSE/authors/id/';
  $Config->{ML_CHOWN_USER}  = 'unsafe';
  $Config->{ML_CHOWN_GROUP} = 'unsafe';
  $Config->{ML_MIN_FILES} = 1;
  $Config->{ML_MIN_INDEX_LINES} = 0;
  $Config->{PAUSE_LOG} = "/home/pause/log/paused.log";
  $Config->{PAUSE_LOG_DIR} = "/home/pause/log/";
  $Config->{PAUSE_PUBLIC_DATA} = '/home/pause/pub/PAUSE/PAUSE-data';
  $Config->{PID_DIR} = "/home/pause/pid/";
  $Config->{TMP} = "/home/pause/tmp/";
  END

sub run_cmd (@args) {
  system {$args[0]} @args;

  croak "failed to run $args[0]" if $?;
}
