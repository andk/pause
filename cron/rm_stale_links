#!/home/pause/.plenv/shims/perl

=pod

misnomer

we do not only remove links but also CHECKSUMS files from directories
that contain nothing but them. And empty directories.

And we call the delfile hook for the CHECKSUMS files. Up to recently
we called the delfile hook with a relative path so the hook dropped it
on the floor:-(

=cut

use FindBin;
use lib "$FindBin::Bin/../lib";
use PAUSE ();

use PAUSE::Logger '$Logger' => { init => {
  ident     => 'pause-rm-stale-links',
  facility  => 'daemon',
} };

use strict;
use warnings;

use File::Find;
use File::Spec;
chdir "$PAUSE::Config->{MLROOT}../..";

my %KEEP_FOREVER = (
  # No matter what, do not delete these paths!  For an example of why this is
  # interesting, imagine a freshly installed PAUSE that has no files uploaded
  # yet.  Without protecting authors/id, it could be deleted.  Later, something
  # will try to upload authors/id/X/XY/XYZZY/Foo-1.23.tar.gz, but it can't,
  # because authors/id has been removed.  Possibly it should be willing to
  # create the full tree, but right now, it can't.  So, we will avoid deleting
  # it instead.
  'authors/id' => 1,
);

find(
     {
      bydepth => 1,
      wanted => sub {
        return if /^\.\.?$/;
        return if $KEEP_FOREVER{ $File::Find::name };
        my($dev,$ino,$mode,$nlink,$uid,$gid) = lstat($_);
        if (-l $_ && ! -e $_){
          $Logger->log("unlinking stale $File::Find::name");
          unlink $_;
          # PAUSE::delfile_hook(File::Spec->rel2abs($_)); # do we track symlinks?
        }
        if ($_ eq "CHECKSUMS") {
          opendir DIR, ".";
          my @readdir = readdir DIR;
          closedir DIR;
          if (@readdir == 3) {
            $Logger->log("unlinking orphaned $File::Find::name");
            unlink $_ or die "Could not unlink $_: $!";
            PAUSE::delfile_hook(File::Spec->rel2abs($_)); # definitely needed
          }
        }
        return if -l $_;
        ($dev,$ino,$mode,$nlink,$uid,$gid) = stat($_);
        if (-d _ and $nlink == 2) { # directory without subdir
          opendir DIR, $_;
          my @readdir = readdir DIR;
          closedir DIR;
          if (@readdir == 2) {
            $Logger->log("rmdirring empty $File::Find::name");
            rmdir $_ or die "Could not rmdir $_: $!";
            # PAUSE::delfile_hook(File::Spec->rel2abs($_)); # we do not track directories
          }
        }
      },
     },
     "authors", "modules");
