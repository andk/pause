#!/usr/local/bin/perl -w -- -*- mode: cperl -*-

=pod

misnomer

we do not only remove links but also CHECKSUMS files from directories
that contain nothing but them. And empty directories.

And we call the delfile hook for the CHECKSUMS files. Up to recently
we called the delfile hook with a relative path so the hook dropped it
on the floor:-(

=cut

use FindBin;
use lib "$FindBin::Bin/../lib";
use PAUSE ();

use strict;

use File::Find;
use File::Spec;
chdir "$PAUSE::Config->{MLROOT}../..";

find(
     {
      bydepth => 1,
      wanted => sub {
        return if /^\.\.?$/;
        my($dev,$ino,$mode,$nlink,$uid,$gid) = lstat($_);
        if (-l $_ && ! -e $_){
          warn "unlinking stale $File::Find::name\n";
          unlink $_;
          # PAUSE::delfile_hook(File::Spec->rel2abs($_)); # do we track symlinks?
        }
        if ($_ eq "CHECKSUMS") {
          opendir DIR, ".";
          my @readdir = readdir DIR;
          closedir DIR;
          if (@readdir == 3) {
            warn "unlinking orphaned $File::Find::name\n";
            unlink $_ or die "Could not unlink $_: $!";
            PAUSE::delfile_hook(File::Spec->rel2abs($_)); # definitely needed
          }
        }
        return if -l $_;
        ($dev,$ino,$mode,$nlink,$uid,$gid) = stat($_);
        if (-d _ and $nlink == 2) { # directory without subdir
          opendir DIR, $_;
          my @readdir = readdir DIR;
          closedir DIR;
          if (@readdir == 2) {
            warn "rmdirring empty $File::Find::name\n";
            rmdir $_ or die "Could not rmdir $_: $!";
            # PAUSE::delfile_hook(File::Spec->rel2abs($_)); # we do not track directories
          }
        }
      },
     },
     "authors", "modules");
